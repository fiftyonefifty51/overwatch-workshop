settings
{
	main
	{
		Description: "Ana Paintball (code ZCEZA), made by twitch.tv/stormyy_ow • lazers, paint effects, jump pads, high scores and TONS of options :D • please import code often to get any updates and fixes • thanks ♥"
		Mode Name: "ana paintball (stormy)"
	}

	lobby
	{
		Allow Players Who Are In Queue: Yes
		Map Rotation: After A Game
		Match Voice Chat: Enabled
		Max Spectators: 12
		Return To Lobby: Never
	}

	modes
	{
		Deathmatch
		{
			enabled maps
			{
				Château Guillard 0
				Château Guillard Halloween 0
			}
		}

		General
		{
			Allow Hero Switching: Off
			Game Mode Start: Immediately
			Hero Limit: Off
			Respawn As Random Hero: On
			Respawn Time Scalar: 90%
			Score To Win: 50
			Spawn Health Packs: Disabled
		}
	}

	heroes
	{
		General
		{
			Ultimate Ability: Off

			Ana
			{
				Biotic Grenade: Off
				Damage Dealt: 500%
				Damage Received: 500%
				Health: 10%
				No Ammunition Requirement: On
				Sleep Dart Cooldown Time: 8%
				Ultimate Ability Nano Boost: Off
			}

			enabled heroes
			{
				Ana
			}
		}
	}

	workshop
	{
		enemy aiming laser dots: Off
		infinite game time: On
		scoping limit: On
	}
}

variables
{
	global:
		1: jump_pads
		2: jump_pads_color
		26: draw_iterator
		27: option_spawnSound
		28: option_gameInfinite
		29: option_aimDots
		30: option_jumpPads
		31: option_deathText
		32: option_gameTips
		33: option_paintBallz
		34: option_aimDotsOnlyWhenScoped
		35: option_playersShowOwnLaser
		36: option_disableMelee
		37: option_disableScoping
		38: option_disableSleepz
		39: option_disableDeath
		40: option_waterForgiveness
		41: option_scopeLimitOn
		42: option_scopeLimitDelay
		43: defaultColors
		44: serverTime

	player:
		2: isFocusing
		3: d
		4: ks
		5: maxks
		6: sleeps
		7: punch
		8: unscopedHits
		24: _paint_ballz
		26: color
		27: _textCache
		28: _effectCache
		29: _bossCache
		30: _timerScoping
		33: tmp_splootPos
		34: tmp_splootOnPlayer
		35: _lastGroundPos
		36: _isAFK
}

subroutines
{
	4: freeze
}

rule("@default @variables")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		"# default colors"
		Global.defaultColors = Empty Array;
		Modify Global Variable(defaultColors, Append To Array, Color(Purple));
		Modify Global Variable(defaultColors, Append To Array, Color(Rose));
		Modify Global Variable(defaultColors, Append To Array, Color(Red));
		Modify Global Variable(defaultColors, Append To Array, Color(Orange));
		Modify Global Variable(defaultColors, Append To Array, Color(Yellow));
		Modify Global Variable(defaultColors, Append To Array, Color(Lime Green));
		Modify Global Variable(defaultColors, Append To Array, Color(Green));
		Modify Global Variable(defaultColors, Append To Array, Color(Turquoise));
		Modify Global Variable(defaultColors, Append To Array, Color(Aqua));
		Modify Global Variable(defaultColors, Append To Array, Color(Sky Blue));
		Modify Global Variable(defaultColors, Append To Array, Color(Blue));
		Modify Global Variable(defaultColors, Append To Array, Color(Violet));
		Modify Global Variable(defaultColors, Append To Array, Color(White));
		disabled Modify Global Variable(defaultColors, Append To Array, Color(Gray));
		disabled Modify Global Variable(defaultColors, Append To Array, Color(Black));
		disabled Modify Global Variable(defaultColors, Append To Array, Color(Team 1));
		disabled Modify Global Variable(defaultColors, Append To Array, Color(Team 2));
		Chase Global Variable Over Time(serverTime, 9999, 9999, None);
	}
}

rule("@world @options")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		"@ options"
		Global.option_spawnSound = Workshop Setting Toggle(Custom String("options"), Custom String("player spawn sound effect"), True, 0);
		Global.option_aimDots = Workshop Setting Toggle(Custom String("options"), Custom String("enemy aiming laser dots"), True, 0);
		Global.option_playersShowOwnLaser = Workshop Setting Toggle(Custom String("options"), Custom String(
			"players can see their own scoping laser"), True, 0);
		Global.option_aimDotsOnlyWhenScoped = Workshop Setting Toggle(Custom String("options"), Custom String(
			"only show aiming lasers when scoped"), True, 0);
		Global.option_deathText = Workshop Setting Toggle(Custom String("options"), Custom String("death text"), False, 0);
		Global.option_paintBallz = Workshop Setting Toggle(Custom String("options"), Custom String("paint ballz"), True, 0);
		"@ game"
		Global.option_gameInfinite = Workshop Setting Toggle(Custom String("game"), Custom String("infinite game time"), False, 0);
		Global.option_waterForgiveness = Workshop Setting Toggle(Custom String("game"), Custom String("players can't die in water"), True,
			0);
		Global.option_jumpPads = Workshop Setting Toggle(Custom String("game"), Custom String("jump pads"), True, 0);
		Global.option_gameTips = Workshop Setting Toggle(Custom String("game"), Custom String("game tips"), True, 0);
		Global.option_disableDeath = Workshop Setting Toggle(Custom String("game"), Custom String("always respawn instantly"), False, 0);
		"@ abilities"
		Global.option_disableMelee = Workshop Setting Toggle(Custom String("abilities"), Custom String("disable melee"), False, 0);
		Global.option_disableScoping = Workshop Setting Toggle(Custom String("abilities"), Custom String("disable scoping"), False, 0);
		Global.option_disableSleepz = Workshop Setting Toggle(Custom String("abilities"), Custom String("disable sleeps"), False, 0);
		Global.option_scopeLimitOn = Workshop Setting Toggle(Custom String("abilities"), Custom String("scoping limit"), False, 0);
		Global.option_scopeLimitDelay = Workshop Setting Real(Custom String("abilities"), Custom String("scoping limit delay"), 0.900,
			0.200, 2, 0);
	}
}

rule("@develop @motd")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		Disable Inspector Recording;
		For Global Variable(J, 0, 14, 1);
			Create HUD Text(Filtered Array(All Players(All Teams), Current Array Element._isAFK), Null, Custom String(" "), Custom String(" "),
				Top, Global.J + 9, Color(White), Color(White), Color(White), Visible To, Default Visibility);
		End;
		Create HUD Text(Filtered Array(All Players(All Teams), Current Array Element._isAFK), Null, Custom String("Ana Paintball"),
			Custom String("• press {0} to join match", Input Binding String(Button(Jump))), Top, 99, Color(White),
			Global.defaultColors[Round To Integer(Global.serverTime % Count Of(Global.defaultColors), Down)], Color(White),
			Visible To String and Color, Default Visibility);
		disabled Create HUD Text(All Players(All Teams), Null, Null, Custom String("THIS MODE IS UNDER DEVELOPMENT"), Left, -9, Color(White), Color(
			White), Color(White), Visible To and String, Default Visibility);
		disabled Create HUD Text(All Players(All Teams), Null, Custom String("please use code \"ZCEZA\" for now, it will be updated"), Null, Left,
			-8, Color(White), Color(White), Color(White), Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Null, Custom String("ana paintball"), Left, -9, Color(White), Color(White), Color(
			White), Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Custom String("twitch.tv/stormyy_ow"), Null, Left, -8, Color(White), Color(White),
			Color(White), Visible To and String, Default Visibility);
		"# mode.focus tip"
		Create HUD Text(Filtered Array(All Players(All Teams), Is Dead(Current Array Element) || Current Array Element._isAFK), Null,
			Custom String("\"Ana Paintball Deluxe\" from stormy"), Custom String("• press {0} to toggle \"focus mode\"",
			Input Binding String(Button(Interact))), Right, 0, Color(White), Global.defaultColors[Round To Integer(
			Global.serverTime % Count Of(Global.defaultColors), Down)], Color(White), Visible To String and Color, Default Visibility);
		"# game code"
		Set Objective Description(All Players(All Teams), Custom String("code: zceza"), Visible To);
		"# infinite timer"
		If(Global.option_gameInfinite);
			Wait(9, Ignore Condition);
			Wait Until(Match Time > 99, 99999);
			Set Match Time(817.500);
			Wait(0.016, Ignore Condition);
			Pause Match Time;
			Disable Built-In Game Mode Completion;
			Disable Built-In Game Mode Announcer;
			Disable Built-In Game Mode Music;
		End;
	}
}

rule("@world @jump_pads")
{
	event
	{
		Ongoing - Global;
	}

	conditions
	{
		Global.option_jumpPads == True;
	}

	actions
	{
		"if you want to add a jump pad, just copy one of the lines below and hit the little camera for the new position. it will do all the work from there :D"
		Global.jump_pads = Empty Array;
		"@make @jump pads"
		If(Current Map == Map(Château Guillard));
			Global.jump_pads_color = Color(Sky Blue);
		Else If(Current Map == Map(Château Guillard Halloween));
			Global.jump_pads_color = Color(Orange);
		Else If(Current Map == Map(Hollywood) || Current Map == Map();
			Global.jump_pads_color = Color(Sky Blue);
		Else If(Map(Kanezaka) == Current Map);
			Global.jump_pads_color = Color(Lime Green);
		End;
		"@make @jump pads"
		If(Current Map == Map(Château Guillard) || Current Map == Map(Château Guillard Halloween));
			Modify Global Variable(jump_pads, Append To Array, Vector(216.272, 2.250, 76.211));
			Modify Global Variable(jump_pads, Append To Array, Vector(187.268, 2.250, 70.981));
			Modify Global Variable(jump_pads, Append To Array, Vector(195.280, 2.250, 80.904));
			Modify Global Variable(jump_pads, Append To Array, Vector(176.989, 1.250, 51.669));
			Modify Global Variable(jump_pads, Append To Array, Vector(206.251, 10.286, 92.463));
			disabled Modify Global Variable(jump_pads, Append To Array, Vector(234.414, 5.250, 104.415));
			Modify Global Variable(jump_pads, Append To Array, Vector(237.398, 3.250, 86.175));
			Modify Global Variable(jump_pads, Append To Array, Vector(230.708, 3.250, 97.343));
			disabled Modify Global Variable(jump_pads, Append To Array, Vector(0, 0, 0));
		Else If(Current Map == Map(Hollywood) || Current Map == Map();
			"behind jail"
			Modify Global Variable(jump_pads, Append To Array, Vector(33.788, 3.358, -79.788));
			"tavern west"
			Modify Global Variable(jump_pads, Append To Array, Vector(13.636, 3.396, -69.146));
			Modify Global Variable(jump_pads, Append To Array, Vector(13.935, 3.379, -66.615));
			"spawn 2a mega"
			Modify Global Variable(jump_pads, Append To Array, Vector(-12.852, 2.978, -63.347));
		Else If(Map(Kanezaka) == Current Map);
			"underpass"
			Modify Global Variable(jump_pads, Append To Array, Vector(-31.461, 7.090, 27.298));
			"middle"
			Modify Global Variable(jump_pads, Append To Array, Vector(-40.777, 7.091, 3.618));
			"alley"
			Modify Global Variable(jump_pads, Append To Array, Vector(-31.536, 10.250, -54.775));
			"bridge"
			Modify Global Variable(jump_pads, Append To Array, Vector(-8.146, 7.250, -25.724));
			"stairs"
			Modify Global Variable(jump_pads, Append To Array, Vector(-5.901, 7.090, 20.850));
		End;
		"@draw @jump pads"
		For Global Variable(draw_iterator, 0, Count Of(Global.jump_pads), 1);
			Create Effect(All Players(All Teams), Cloud, Global.jump_pads_color, Global.jump_pads[Global.draw_iterator] + Vector(0, -1.600, 0),
				0.400, Visible To);
			Create Effect(All Players(All Teams), Sparkles, Global.jump_pads_color, Global.jump_pads[Global.draw_iterator] + Vector(0, -1.600,
				0), 0.900, Visible To);
			Create Effect(All Players(All Teams), Sphere, Global.jump_pads_color, Global.jump_pads[Global.draw_iterator] + Vector(0, -2.900,
				0), 2, Visible To);
		End;
	}
}

rule("TODO: optimize # @player @jump_pads")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.option_jumpPads == True;
		Is Alive(Event Player) == True;
		(Is On Ground(Event Player) || Vertical Speed Of(Event Player) < -4) == True;
		Is True For Any(Global.jump_pads, Distance Between(Event Player, Current Array Element + Vector(0, -1.600, 0)) <= 1.200) == True;
	}

	actions
	{
		Apply Impulse(Event Player, Up, 16.800 + Absolute Value(Vertical Speed Of(Event Player)) / 4, To World, Cancel Contrary Motion);
		Play Effect(All Players(All Teams), Ring Explosion Sound, Color(Sky Blue), Position Of(Event Player), 40);
		Play Effect(All Players(All Teams), Ring Explosion, Global.jump_pads_color, Position Of(Event Player), 2);
		Play Effect(All Players(All Teams), Good Pickup Effect, Global.jump_pads_color, Position Of(Event Player), 0.900);
	}
}

rule("@player @join")
{
	event
	{
		Player Joined Match;
		All;
		All;
	}

	actions
	{
		Event Player._textCache = Empty Array;
		Event Player._effectCache = Empty Array;
		Event Player._bossCache = Empty Array;
		Event Player.isFocusing = True;
		Disable Hero HUD(Event Player);
		Disable Game Mode HUD(Event Player);
		Event Player._isAFK = True;
		"# hide player"
		Set Status(Event Player, Null, Phased Out, 9999);
		disabled Set Status(Event Player, Null, Rooted, 9999);
		"your own hero shows up invisible on camera :("
		disabled Set Invisible(Event Player, Enemies);
		"# wait until they spawn"
		Wait Until(Has Spawned(Event Player), 99999);
		Start Holding Button(Event Player, Button(Secondary Fire));
		Start Forcing Player Outlines(Event Player, Event Player, True, Color(Purple), Default);
		"# kanezaka"
		If(Map(Kanezaka) == Current Map);
			Start Forcing Player Position(Event Player, Vector(-61.540, 8.300, 24.603), True);
			Wait(0.016, Ignore Condition);
			Start Facing(Event Player, Direction Towards(Eye Position(Event Player), Vector(-64.511, 8.400, 23.488)), 900, To World,
				Direction and Turn Rate);
			disabled Teleport(Event Player, Vector(195.904, 23.600, 82.410));
			Start Camera(Event Player, Vector(-62.434, 9.250, 23.442), Vector(-60.723, 10.900, 31.356), 90);
		"# chateau guillard"
		Else;
			Start Facing(Event Player, Direction Towards(Eye Position(Event Player), Vector(198.139, 24.755, 84.200)), 900, To World,
				Direction and Turn Rate);
			Start Forcing Player Position(Event Player, Vector(195.904, 23.600, 82.410), True);
			disabled Teleport(Event Player, Vector(195.904, 23.600, 82.410));
			Start Camera(Event Player, Vector(195.860, 24.755, 83.780), Vector(198.600, 26, 77.436), 90);
		End;
		"@player @color select"
		Event Player.color = Random Value In Array(Global.defaultColors);
		Create Progress Bar HUD Text(Event Player, (Index Of Array Value(Global.defaultColors, Event Player.color) + 1) / Count Of(
			Global.defaultColors) * 100, Custom String("{0}  {1}  {2}", Index Of Array Value(Global.defaultColors, Event Player.color)
			== 0 ? Custom String("") : Icon String(Arrow: Left), Event Player.color, Event Player.color == Last Of(Global.defaultColors)
			? Custom String("") : Icon String(Arrow: Right)), Top, 9, Event Player.color, Color(White), Visible To Values and Color,
			Default Visibility);
		Modify Player Variable(Event Player, _bossCache, Append To Array, Last Text ID);
		"effect"
		Create Effect(Event Player, Good Aura, Event Player.color, Event Player, 4, Visible To and Color);
		Modify Player Variable(Event Player, _effectCache, Append To Array, Last Created Entity);
		"text"
		Create In-World Text(Event Player, Custom String("PRESS {0} TO JOIN MATCH", Input Binding String(Button(Jump))), Vector(198.600,
			22.600, 77.436), 12, Do Not Clip, Visible To and String, Color(White), Default Visibility);
		Modify Player Variable(Event Player, _textCache, Append To Array, Last Text ID);
		"effect"
		Create Effect(Event Player, Cloud, Event Player.color, Event Player, 4, Visible To and Color);
		Modify Player Variable(Event Player, _effectCache, Append To Array, Last Created Entity);
		"@"
		disabled Wait(2, Ignore Condition);
		disabled Stop Facing(Event Player);
		"# color select"
		While(0 == 0);
			Wait Until(Is Button Held(Event Player, Button(Jump)) || Absolute Value(X Component Of(Throttle Of(Event Player))) > 0.400, 6);
			If(Is Button Held(Event Player, Button(Jump)));
				Break;
			Else If(X Component Of(Throttle Of(Event Player)) < 0);
				Event Player.color = Global.defaultColors[(Index Of Array Value(Global.defaultColors, Event Player.color) + 1) % Count Of(
					Global.defaultColors)];
			Else;
				Event Player.color = Global.defaultColors[Index Of Array Value(Global.defaultColors, Event Player.color)
					!= 0 ? Index Of Array Value(Global.defaultColors, Event Player.color) - 1 : Count Of(Global.defaultColors) - 1];
			End;
			Wait Until(!(Is Button Held(Event Player, Button(Jump)) || Absolute Value(X Component Of(Throttle Of(Event Player))) > 0.400), 6);
			disabled Wait(0.090, Ignore Condition);
		End;
		"@color is selected"
		Stop Camera(Event Player);
		Clear Status(Event Player, Phased Out);
		Stop Holding Button(Event Player, Button(Secondary Fire));
		Stop Facing(Event Player);
		Stop Forcing Player Position(Event Player);
		Wait(0.016, Ignore Condition);
		Teleport(Event Player, Random Value In Array(Spawn Points(All Teams)));
		"# wipe text cache"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._textCache), 1);
			"text"
			disabled Create In-World Text(Event Player, Event Player.color, Eye Position(Event Player) + Facing Direction Of(Event Player) * 1 + Vector(
				0, -0.200, 0), 20, Clip Against Surfaces, Visible To Position String and Color, Event Player.color, Default Visibility);
			disabled Modify Player Variable(Event Player, _textCache, Append To Array, Last Text ID);
			Destroy In-World Text(Event Player._textCache[Event Player.X]);
		End;
		"# wipe effect cache"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._effectCache), 1);
			Destroy Effect(Event Player._effectCache[Event Player.X]);
		End;
		"# wipe bossbar cache"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._bossCache), 1);
			Destroy Progress Bar HUD Text(Event Player._bossCache[Event Player.X]);
		End;
		Enable Hero HUD(Event Player);
		Enable Game Mode HUD(Event Player);
		If(Global.option_scopeLimitOn);
			"scope timer boss bar"
			Create Progress Bar HUD Text(Event Player._timerScoping > 0 && Is Firing Secondary(Event Player) ? Event Player : Null,
				Round To Integer(Event Player._timerScoping / Global.option_scopeLimitDelay * 100, Up), Custom String("scoping timer"), Top, 9,
				Event Player.color, Color(White), Visible To Values and Color, Default Visibility);
			Modify Player Variable(Event Player, _bossCache, Append To Array, Last Text ID);
		End;
		If(Global.option_aimDots);
			"# player dot"
			Create Effect(Global.option_playersShowOwnLaser ? All Players(All Teams) : Filtered Array(All Players(All Teams),
				Event Player != Current Array Element), Good Aura, Event Player.color, Update Every Frame((
				!Global.option_aimDotsOnlyWhenScoped ? True : Is Firing Secondary(Event Player)) && !(Ray Cast Hit Position(Eye Position(
				Event Player), Eye Position(Event Player) + Facing Direction Of(Event Player) * 90, All Players(All Teams), Event Player,
				False) == Eye Position(Event Player) + Facing Direction Of(Event Player) * 90) ? Ray Cast Hit Position(Eye Position(
				Event Player), Eye Position(Event Player) + Facing Direction Of(Event Player) * 90, All Players(All Teams), Event Player,
				False) : Null), 0.049, Visible To Position Radius and Color);
			disabled Wait Until(Is Button Held(Event Player, Button(Jump)) || Absolute Value(X Component Of(Throttle Of(Event Player))) > 0.400, 6);
			Modify Player Variable(Event Player, _effectCache, Append To Array, Last Created Entity);
		End;
		"# show new color to all other players"
		For Player Variable(Event Player, J, 0, Count Of(All Players(All Teams)), 1);
			Start Forcing Player Outlines(Event Player, All Players(All Teams)[Event Player.J], True, Event Player.color, Default);
		End;
		Event Player._isAFK = False;
		Event Player.isFocusing = False;
	}
}

disabled rule("")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Interact)) == True;
		Event Player == Host Player;
	}

	actions
	{
		Small Message(Event Player, Custom String("noclip"));
		While(Is Button Held(Event Player, Button(Interact)));
			If(!Is Button Held(Event Player, Button(Ability 1)));
				Start Forcing Player Position(Event Player, Position Of(Event Player) + Facing Direction Of(Event Player) * 0.200, False);
			End;
			Wait(0.016, Ignore Condition);
		End;
		Stop Forcing Player Position(Event Player);
		Wait(0.090, Ignore Condition);
		Apply Impulse(Event Player, Vector(0, 9, 0), 0, To World, Cancel Contrary Motion);
	}
}

rule("@player @timer.scoping")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Firing Secondary(Event Player) == True;
		Global.option_scopeLimitOn == True;
	}

	actions
	{
		"# count down timer to 0"
		Event Player._timerScoping = Global.option_scopeLimitDelay;
		Chase Player Variable Over Time(Event Player, _timerScoping, 0, Global.option_scopeLimitDelay, Destination and Duration);
		"# wait until timer is done"
		Wait(Global.option_scopeLimitDelay, Abort When False);
		disabled Wait(0.200, Abort When False);
		Wait(0.200, Ignore Condition);
		Abort If(!Is Firing Secondary(Event Player));
		Cancel Primary Action(Event Player);
	}
}

rule("@player @paint.ballz")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Firing Primary(Event Player) == True;
		Global.option_paintBallz == True;
	}

	actions
	{
		"if no player and wall was hit"
		Abort If(Ray Cast Hit Position(Eye Position(Event Player), Eye Position(Event Player) + Facing Direction Of(Event Player) * 90,
			All Players(All Teams), Event Player, False) == Eye Position(Event Player) + Facing Direction Of(Event Player) * 90);
		Event Player.tmp_splootOnPlayer = Ray Cast Hit Player(Eye Position(Event Player), Eye Position(Event Player) + Facing Direction Of(
			Event Player) * 90, All Players(All Teams), Event Player, False) != Null;
		"@note \"can delete this variable if a tertiary ray cast is done on point of impact -> boolean\""
		Event Player.tmp_splootPos = Ray Cast Hit Position(Eye Position(Event Player), Eye Position(Event Player) + Facing Direction Of(
			Event Player) * 99, All Players(All Teams), Event Player, False);
		"# secondary delay"
		If(!Is Firing Secondary(Event Player));
			"#previously 90"
			Wait(Distance Between(Eye Position(Event Player), Event Player.tmp_splootPos) / 125, Ignore Condition);
		End;
		"# paint sploot"
		Play Effect(All Players(All Teams), Bad Explosion, Event Player.color, Event Player.tmp_splootPos, 0.200);
		Abort If(Event Player.tmp_splootOnPlayer);
		"# paint ball"
		Create Effect(All Players(All Teams), Sphere, Event Player.color, Event Player.tmp_splootPos, 0.090, Visible To and Color);
		Modify Player Variable(Event Player, _paint_ballz, Append To Array, Last Created Entity);
	}
}

rule("@player @quit")
{
	event
	{
		Player Left Match;
		All;
		All;
	}

	actions
	{
		"# wipe text cache"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._textCache), 1);
			Destroy In-World Text(Event Player._textCache[Event Player.X]);
		End;
		"# wipe effect cache"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._effectCache), 1);
			Destroy Effect(Event Player._effectCache[Event Player.X]);
		End;
		"# wipe bossbar cache"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._bossCache), 1);
			Destroy Progress Bar HUD Text(Event Player._bossCache[Event Player.X]);
		End;
	}
}

rule("@player @death")
{
	event
	{
		Player Died;
		All;
		All;
	}

	actions
	{
		"# wipe paintballz"
		For Player Variable(Event Player, X, 0, Count Of(Event Player._paint_ballz), 1);
			Destroy Effect(Event Player._paint_ballz[Event Player.X]);
		End;
	}
}

disabled rule("TODO: \"COMBINED sorted array of paint ball ids\"")
{
	event
	{
		Ongoing - Global;
	}
}

rule("@player.join @outlines")
{
	event
	{
		Player Joined Match;
		All;
		All;
	}

	actions
	{
		Wait Until(Has Spawned(Event Player), 99999);
		"# download color data for other players"
		For Player Variable(Event Player, J, 0, Count Of(All Players(All Teams)), 1);
			Start Forcing Player Outlines(All Players(All Teams)[Event Player.J], Event Player, True, All Players(All Teams)
				[Event Player.J].color, Default);
		End;
	}
}

rule("@player.death @quick")
{
	event
	{
		Player Died;
		All;
		All;
	}

	actions
	{
		If(Global.option_disableDeath);
			Respawn(Event Player);
			Abort;
		End;
		Wait Until(!Is Button Held(Event Player, Button(Jump)), 99999);
		Wait Until(Is Button Held(Event Player, Button(Jump)), 99999);
		Abort If(!Is Dead(Event Player));
		Respawn(Event Player);
	}
}

rule("@player.spawn @sound.effect")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.option_spawnSound == True;
		Is Alive(Event Player) == True;
		Event Player._isAFK == False;
	}

	actions
	{
		Wait Until(Absolute Value(X Component Of(Throttle Of(Event Player))) + Absolute Value(Y Component Of(Throttle Of(Event Player)))
			+ Absolute Value(Z Component Of(Throttle Of(Event Player))) > 0.090, 99999);
		Play Effect(All Players(All Teams), Explosion Sound, Color(White), Position Of(Event Player), 80);
	}
}

rule("@world @gameinfo")
{
	event
	{
		Ongoing - Global;
	}

	actions
	{
		"# sparkle effects"
		disabled Create Effect(All Players(All Teams), Sparkles, Color(Sky Blue), Position Of(Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.sleeps))), 0.900, Visible To Position and Radius);
		disabled Create Effect(All Players(All Teams), Sparkles, Color(Purple), Position Of(Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.maxks))), 0.900, Visible To Position and Radius);
		disabled Create Effect(All Players(All Teams), Sparkles, Color(Red), Position Of(Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.d))), 1, Visible To Position and Radius);
		disabled Create Effect(All Players(All Teams), Sparkles, Color(Red), Position Of(Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.punch))), 0.900, Visible To Position and Radius);
		disabled Create Effect(All Players(All Teams), Sparkles, Color(Orange), Position Of(Last Of(Sorted Array(All Players(All Teams),
			Player Stat(Current Array Element, Final Blows) / Player Stat(Current Array Element, Deaths)))), 0.900,
			Visible To Position and Radius);
		If(!Global.option_disableSleepz);
			"# game board"
			Create HUD Text(All Players(All Teams), Null, Custom String(" "), Null, Left, 0, Color(White), Color(White), Color(White),
				Visible To and String, Default Visibility);
			Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Ana), Button(
				Ability 1)), Custom String("most sleepz"), Custom String("{0} - {2}", Last Of(Sorted Array(All Players(All Teams),
				Current Array Element.sleeps)), Null, Last Of(Sorted Array(All Players(All Teams), Current Array Element.sleeps)).sleeps),
				Left, 1, Custom Color(254, 163, 170, 255), Custom Color(254, 163, 170, 255), Color(White), Visible To and String,
				Default Visibility);
		End;
		Create HUD Text(All Players(All Teams), Null, Custom String("  "), Null, Left, 2, Color(White), Color(White), Color(White),
			Visible To and String, Default Visibility);
		Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Pharah),
			Button(Ability 1)), Custom String("higest killstreak"), Custom String("{0} - {2}", Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.maxks)), Null, Last Of(Sorted Array(All Players(All Teams), Current Array Element.maxks)).maxks), Left,
			3, Custom Color(248, 184, 139, 255), Custom Color(248, 184, 139, 255), Color(White), Visible To and String,
			Default Visibility);
		disabled Create HUD Text(All Players(All Teams), Null, Custom String("  "), Null, Left, 4, Color(White), Color(White), Color(White),
			Visible To and String, Default Visibility);
		disabled Create HUD Text(All Players(All Teams), Icon String(Skull), Custom String("most deaths"), Custom String("{0} - {2}", Last Of(
			Sorted Array(All Players(All Teams), Current Array Element.d)), Null, Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.d)).d), Left, 5, Color(Red), Color(Red), Color(White), Visible To and String, Default Visibility);
		If(!Global.option_disableMelee);
			Create HUD Text(All Players(All Teams), Null, Custom String("  "), Null, Left, 4, Color(White), Color(White), Color(White),
				Visible To and String, Default Visibility);
			Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Ana), Button(
				Melee)), Custom String("most melees"), Custom String("{0} - {2}", Last Of(Sorted Array(All Players(All Teams),
				Current Array Element.punch)), Null, Last Of(Sorted Array(All Players(All Teams), Current Array Element.punch)).punch), Left,
				5, Custom Color(250, 248, 132, 255), Custom Color(250, 248, 132, 255), Color(White), Visible To and String,
				Default Visibility);
		End;
		Create HUD Text(All Players(All Teams), Null, Custom String("  "), Null, Left, 6, Color(White), Color(White), Color(White),
			Visible To and String, Default Visibility);
		Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Mercy), Button(
			Ultimate)), Custom String("higest k/d"), Custom String("{0} - {2}", Last Of(Sorted Array(All Players(All Teams), Player Stat(
			Current Array Element, Final Blows) / Player Stat(Current Array Element, Deaths))), Null, Player Stat(Last Of(Sorted Array(
			All Players(All Teams), Player Stat(Current Array Element, Final Blows) / Player Stat(Current Array Element, Deaths))),
			Final Blows) / Player Stat(Last Of(Sorted Array(All Players(All Teams), Player Stat(Current Array Element, Final Blows)
			/ Player Stat(Current Array Element, Deaths))), Deaths)), Left, 7, Custom Color(186, 237, 145, 255), Custom Color(186, 237,
			145, 255), Color(White), Visible To and String, Default Visibility);
		Create HUD Text(All Players(All Teams), Null, Custom String(" "), Null, Left, 8, Color(White), Color(White), Color(White),
			Visible To and String, Default Visibility);
		disabled Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Ana), Button(
			Ability 1)), Custom String("most noscopes"), Custom String("{0} - {2}", Last Of(Sorted Array(All Players(All Teams),
			Current Array Element.unscopedHits)), Null, Last Of(Sorted Array(All Players(All Teams), Current Array Element.unscopedHits))
			.unscopedHits), Left, 9, Custom Color(178, 206, 254, 255), Custom Color(178, 206, 254, 255), Color(White),
			Visible To and String, Default Visibility);
		Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Baptiste),
			Button(Ability 2)), Custom String("most noscopes"), Custom String("{0} - {2}", Last Of(Sorted Array(All Players(All Teams),
			Player Hero Stat(Current Array Element, Hero(Ana), Shots Hit) - Player Hero Stat(Current Array Element, Hero(Ana),
			Scoped Hits))), Null, Player Hero Stat(Last Of(Sorted Array(All Players(All Teams), Player Hero Stat(Current Array Element,
			Hero(Ana), Shots Hit) - Player Hero Stat(Current Array Element, Hero(Ana), Scoped Hits))), Hero(Ana), Shots Hit)
			- Player Hero Stat(Last Of(Sorted Array(All Players(All Teams), Player Hero Stat(Current Array Element, Hero(Ana), Shots Hit)
			- Player Hero Stat(Current Array Element, Hero(Ana), Scoped Hits))), Hero(Ana), Scoped Hits)), Left, 9, Custom Color(178, 206,
			254, 255), Custom Color(178, 206, 254, 255), Color(White), Visible To and String, Default Visibility);
		If(!Global.option_disableScoping);
			Create HUD Text(All Players(All Teams), Null, Custom String(" "), Null, Left, 10, Color(White), Color(White), Color(White),
				Visible To and String, Default Visibility);
			Create HUD Text(Filtered Array(All Players(All Teams), !Current Array Element.isFocusing), Ability Icon String(Hero(Echo), Button(
				Secondary Fire)), Custom String("scoped accuracy"), Custom String("{0} - {2}%", Last Of(Sorted Array(All Players(All Teams),
				Player Hero Stat(Current Array Element, Hero(Ana), Scoped Accuracy))), Null, Round To Integer(Player Hero Stat(Last Of(
				Sorted Array(All Players(All Teams), Player Hero Stat(Current Array Element, Hero(Ana), Scoped Accuracy))), Hero(Ana),
				Scoped Accuracy) * 100, Up)), Left, 12, Custom Color(242, 162, 232, 255), Custom Color(242, 162, 232, 255), Color(White),
				Visible To and String, Default Visibility);
		End;
	}
}

rule("@player @track#kills")
{
	event
	{
		Player Dealt Final Blow;
		All;
		All;
	}

	actions
	{
		"@track#sleeps"
		If(Event Ability == Button(Ability 1));
			Play Effect(All Players(All Teams), Good Pickup Effect, Custom Color(254, 163, 170, 255), Event Player, 1);
			Event Player.sleeps += 1;
			If(Random Integer(0, 1) < 1);
				Communicate(Attacker, Sorry);
			End;
		End;
		"@track#punches"
		If(Event Ability == Button(Melee));
			Play Effect(All Players(All Teams), Good Pickup Effect, Custom Color(250, 248, 132, 255), Event Player, 1);
			Event Player.punch += 1;
			If(Random Integer(0, 1) < 1);
				Communicate(Attacker, Goodbye);
			End;
		End;
		"@track#ks"
		Event Player.ks += 1;
		"@track#maxks"
		If(Event Player.ks > Event Player.maxks);
			Play Effect(All Players(All Teams), Good Pickup Effect, Custom Color(248, 184, 139, 255), Event Player, 1);
			Event Player.maxks = Event Player.ks;
		End;
		disabled Event Player.unscopedHits = Player Hero Stat(Event Player, Hero(Ana), Shots Hit) - Player Hero Stat(Event Player, Hero(Ana),
			Scoped Hits);
		Victim.ks = 0;
	}
}

rule("@player @mode.focus")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Is Button Held(Event Player, Button(Interact)) == True;
		Event Player._isAFK == False;
	}

	actions
	{
		Event Player.isFocusing = !Event Player.isFocusing;
		Small Message(Event Player, Custom String("focus mode: {2}", Null, Null, Event Player.isFocusing ? Custom String("on")
			: Custom String("off")));
		"# hero display"
		If(Event Player.isFocusing);
			Disable Game Mode HUD(Event Player);
		Else;
			Enable Game Mode HUD(Event Player);
		End;
	}
}

rule("@player @disable")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	actions
	{
		If(Global.option_disableMelee);
			Set Melee Enabled(Event Player, False);
		End;
		If(Global.option_disableScoping);
			Set Secondary Fire Enabled(Event Player, False);
		End;
		If(Global.option_disableSleepz);
			Set Ability 1 Enabled(Event Player, False);
		End;
	}
}

disabled rule("@water @player.boat")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Global.option_waterForgiveness == True;
	}

	actions
	{
		Wait Until(Has Spawned(Event Player) && Is On Ground(Event Player), 99999);
		Event Player._lastGroundPos = Position Of(Event Player);
		"# event loop"
		While(0 == 0);
			Wait Until(!Is On Ground(Event Player), 99999);
			Event Player._lastGroundPos = Position Of(Event Player);
			"# wait until is swimming in water"
			Wait Until((!Is On Ground(Event Player) && Y Component Of(Update Every Frame(Position Of(Event Player))) < -0.200) || Is On Ground(
				Event Player), 99999);
			If(Is On Ground(Event Player));
				Continue;
			End;
			"# wait until is swimming in water"
			disabled Wait Until(!Is On Ground(Event Player) && Y Component Of(Update Every Frame(Position Of(Event Player))) < -0.200, 99999);
			Wait(0.016, Ignore Condition);
			If(Is On Ground(Event Player));
				Continue;
			End;
			Teleport(Event Player, Event Player._lastGroundPos);
			Apply Impulse(Event Player, Vector Towards(Velocity Of(Event Player), Vector(0, 0, 0)), 0.002, To World, Cancel Contrary Motion);
			Set Status(Event Player, Null, Frozen, 2);
			Wait(0.016, Ignore Condition);
			Wait Until(Absolute Value(Vertical Speed Of(Event Player)) > 9, 2);
			If(Vertical Speed Of(Event Player) > 9);
				Abort;
			End;
			If(Has Status(Event Player, Frozen));
				Teleport(Event Player, Nearest Walkable Position(Event Player._lastGroundPos));
			End;
		End;
	}
}

rule("@water @player.boat @map.chateau @map.chateau.halloween")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		"# is chateau (this glitches on other maps :c"
		Array Contains(Array(Map(Château Guillard), Map(Château Guillard Halloween)), Current Map) == True;
		Global.option_waterForgiveness == True;
		"is in air"
		Is On Ground(Event Player) == False;
		"is falling"
		Vertical Speed Of(Event Player) < 0;
		"# optional quality verification"
		Distance Between(Position Of(Event Player), Nearest Walkable Position(Position Of(Event Player))) > 2;
		Y Component Of(Update Every Frame(Position Of(Event Player))) < -0.200;
	}

	actions
	{
		Apply Impulse(Event Player, Vector Towards(Velocity Of(Event Player), Vector(0, 0, 0)), 0.002, To World, Cancel Contrary Motion);
		Teleport(Event Player, Event Player._lastGroundPos);
		Start Forcing Player Outlines(Event Player, All Players(All Teams), True, Event Player.color, Occluded);
		Set Status(Event Player, Null, Frozen, 2);
		Wait Until(Y Component Of(Update Every Frame(Position Of(Event Player))) < 0.200, 2.900);
		If(Has Status(Event Player, Frozen));
			Apply Impulse(Event Player, Vector Towards(Velocity Of(Event Player), Vector(0, 0, 0)), 0.002, To World, Cancel Contrary Motion);
			Teleport(Event Player, Nearest Walkable Position(Event Player._lastGroundPos));
		End;
		Start Forcing Player Outlines(Event Player, All Players(All Teams), True, Event Player.color, Default);
	}
}

rule("@player @variable.lastGroundPos")
{
	event
	{
		Ongoing - Each Player;
		All;
		All;
	}

	conditions
	{
		Event Player._isAFK == False;
		Is On Ground(Event Player) == False;
	}

	actions
	{
		Event Player._lastGroundPos = Position Of(Event Player);
	}
}

rule("@method @freeze")
{
	event
	{
		Subroutine;
		freeze;
	}

	actions
	{
		Apply Impulse(Event Player, Up, 0.001, To World, Cancel Contrary Motion);
		Apply Impulse(Event Player, Down, 0.001, To World, Cancel Contrary Motion);
		Apply Impulse(Event Player, Left, 0.001, To World, Cancel Contrary Motion);
		Apply Impulse(Event Player, Right, 0.001, To World, Cancel Contrary Motion);
		Apply Impulse(Event Player, Forward, 0.001, To World, Cancel Contrary Motion);
		Apply Impulse(Event Player, Backward, 0.001, To World, Cancel Contrary Motion);
	}
}
